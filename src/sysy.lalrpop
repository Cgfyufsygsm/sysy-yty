// lalrpop 里的约定
grammar;

use crate::frontend::ast::*;

// 约束 lexer 的行为
match {
  // 跳过空白符和注释
  r"\s*" => {},
  r"//[^\n\r]*[\n\r]*" => {},
  // 添加多行注释的处理
  r"/\*([^*]|\*[^/])*\*/" => {},

  r"if" => "if",
  r"else" => "else",
  r"while" => "while",
  r"continue" => "continue",
  r"break" => "break",
  r"int" => "int",
  r"void" => "void",
  r"return" => "return",

  // 剩下的情况采用默认方式处理
  _
}

pub CompUnit: CompUnit = {
  <comp_items: CompItem*> => CompUnit { <> }
}

CompItem: CompItem = {
  <FuncDef> => CompItem::FuncDef(<>),
  <Decl> => CompItem::Decl(<>)
}

FuncDef: FuncDef = {
  <btype: BType> <ident: Ident> "(" <params: Comma<FuncParam>> ")" <block: Block> => FuncDef { <> }
}

FuncParam: FuncParam = {
  <btype: BType> <ident: Ident> => FuncParam { <> }
}

Block: Block = "{" <items: BlockItem*> "}" => Block { <> };

BlockItem: BlockItem = {
  <Decl> => BlockItem::Decl(<>),
  <Stmt> => BlockItem::Stmt(<>),
};

Decl: Decl = {
  <ConstDecl> => Decl::Const(<>),
  <VarDecl> => Decl::Var(<>),
};

ConstDecl: ConstDecl = {
  "const" <btype: BType> <defs: Comma<ConstDef>> ";" => {
    ConstDecl { <> }
  }
};

VarDecl: VarDecl = {
  <btype: BType> <defs: Comma<VarDef>> ";" => {
    VarDecl { <> }
  }
};

BType: BType = {
  "void" => BType::Void,
  "int" => BType::Int,
};

ConstDef: ConstDef = {
  <ident: Ident> "=" <init: ConstInitVal> => ConstDef { <> },
};

VarDef: VarDef = {
  <ident: Ident> => VarDef { ident, init: None },
  <ident: Ident> "=" <init: InitVal> => VarDef { ident, init: Some(init) },
};

ConstInitVal: ConstInitVal = {
  <ConstExp> => ConstInitVal::Exp(<>),
};

InitVal: InitVal = {
  <Exp> => InitVal::Exp(<>),
};

ConstExp: Exp = Exp;

Stmt: Stmt = {
  <MatchedStmt> => <>,
  <UnmatchedStmt> => <>
};

MatchedStmt: Stmt = {
  // 不含 if/else 的也包含在这里
  "return" <Exp?> ";" => Stmt::Return(<>),
  <lval: LVal> "=" <exp: Exp> ";" => Stmt::Assign { <> },
  <Exp?> ";" => Stmt::Exp(<>),
  <Block> => Stmt::Block(<>),
  "break" ";" => Stmt::Break(Break),
  "continue" ";" => Stmt::Continue(Continue),

  "if" "(" <cond: Exp> ")" <then_: MatchedStmt> "else" <else_: MatchedStmt> => {
    Stmt::If(If { cond, then_block: Box::new(then_), else_block: Some(Box::new(else_)) })
  },

  "while" "(" <cond: Exp> ")" <body: MatchedStmt> => {
    Stmt::While(While { cond, body: Box::new(body) })
  },
}

UnmatchedStmt: Stmt = {
  "while" "(" <cond: Exp> ")" <body: UnmatchedStmt> => {
    Stmt::While(While { cond, body: Box::new(body) })
  },
  // if without else
  "if" "(" <cond: Exp> ")" <then_: Stmt> => {
    Stmt::If( If { cond, then_block: Box::new(then_), else_block: None })
  },
  // if (cond) MatchedStmt else UnmatchedStmt
  // （用于表达 "if (...) matched_stmt else unmatched_stmt" 的情形）
  "if" "(" <cond: Exp> ")" <then_m: MatchedStmt> "else" <u: UnmatchedStmt>
    => Stmt::If(If { cond, then_block: Box::new(then_m), else_block: Some(Box::new(u)) }),
}


// ------------ Expression Parsing ------------

Exp: Exp = LOrExp;

// LOrExp := LAndExp | LOrExp "||" LAndExp
LOrExp: Exp = {
  <lhs: LOrExp> "||" <rhs: LAndExp> => Exp::ShortCircuit { op: ShortCircuitOp::Or, lhs: Box::new(lhs), rhs: Box::new(rhs) },
  <LAndExp> => <>,
};

// LAndExp := EqExp | LAndExp "&&" EqExp
LAndExp: Exp = {
  <lhs: LAndExp> "&&" <rhs: EqExp> => Exp::ShortCircuit { op: ShortCircuitOp::And, lhs: Box::new(lhs), rhs: Box::new(rhs) },
  <EqExp> => <>,
};

// EqExp := RelExp | EqExp ("==" | "!=") RelExp
EqExp: Exp = {
  <lhs: EqExp> "==" <rhs: RelExp> => Exp::Binary { op: BinaryOp::Eq, lhs: Box::new(lhs), rhs: Box::new(rhs) },
  <lhs: EqExp> "!=" <rhs: RelExp> => Exp::Binary { op: BinaryOp::Ne, lhs: Box::new(lhs), rhs: Box::new(rhs) },
  <RelExp> => <>,
};

// RelExp := AddExp | RelExp ("<" | ">" | "<=" | ">=") AddExp
RelExp: Exp = {
  <lhs: RelExp> "<"  <rhs: AddExp> => Exp::Binary { op: BinaryOp::Lt, lhs: Box::new(lhs), rhs: Box::new(rhs) },
  <lhs: RelExp> ">"  <rhs: AddExp> => Exp::Binary { op: BinaryOp::Gt, lhs: Box::new(lhs), rhs: Box::new(rhs) },
  <lhs: RelExp> "<=" <rhs: AddExp> => Exp::Binary { op: BinaryOp::Le, lhs: Box::new(lhs), rhs: Box::new(rhs) },
  <lhs: RelExp> ">=" <rhs: AddExp> => Exp::Binary { op: BinaryOp::Ge, lhs: Box::new(lhs), rhs: Box::new(rhs) },
  <AddExp> => <>,
};

AddExp: Exp = {
  <m: MulExp> => m,
  <lhs: AddExp> "+" <rhs: MulExp> => Exp::Binary { op: BinaryOp::Add, lhs: Box::new(lhs), rhs: Box::new(rhs) },
  <lhs: AddExp> "-" <rhs: MulExp> => Exp::Binary { op: BinaryOp::Sub, lhs: Box::new(lhs), rhs: Box::new(rhs) },
}

MulExp: Exp = {
  <lhs: MulExp> "*" <rhs: UnaryExp> => Exp::Binary { op: BinaryOp::Mul, lhs: Box::new(lhs), rhs: Box::new(rhs) },
  <lhs: MulExp> "/" <rhs: UnaryExp> => Exp::Binary { op: BinaryOp::Div, lhs: Box::new(lhs), rhs: Box::new(rhs) },
  <lhs: MulExp> "%" <rhs: UnaryExp> => Exp::Binary { op: BinaryOp::Mod, lhs: Box::new(lhs), rhs: Box::new(rhs) },
  <UnaryExp> => <>,
}

UnaryExp: Exp = {
  // 函数调用优先于 PrimaryExp（避免把 IDENT 当 LVal 解析）
  <PrimaryExp> => <>,
  <FuncCall> => Exp::Call(<>),
  <op: UnaryOp> <rhs: UnaryExp> => Exp::Unary { op, exp: Box::new(rhs) },
}

FuncCall: Call = {
  <func: Ident> "(" <args: Comma<Exp>> ")" => Call { <> }
}

PrimaryExp: Exp = {
  "(" <Exp> ")" => <>,
  <Number> => Exp::Number(<>),
  <LVal> => Exp::LVal(<>),
};

LVal: LVal = {
  <Ident> => LVal::Var(<>),
};

UnaryOp: UnaryOp = { 
  "+" => UnaryOp::Pos,
  "-" => UnaryOp::Neg,
  "!" => UnaryOp::Not,
}

Number: i32 = <IntConst> => <>;

// 如果匹配到标识符, 就返回这个字符串
// 一对尖括号在此处指代的是正则表达式匹配到的字符串 (&str)
// 关于尖括号到底代表什么, 请 RTFM
Ident: String = r"[_a-zA-Z][_a-zA-Z0-9]*" => <>.to_string();

// 对整数字面量的处理方式: 把匹配到的字符串按对应进制转换成数字
IntConst: i32 = {
  r"[1-9][0-9]*" => i32::from_str_radix(<>, 10).unwrap(),
  r"0[0-7]*" => i32::from_str_radix(<>, 8).unwrap(),
  r"0[xX][0-9a-fA-F]+" => i32::from_str_radix(&<>[2..], 16).unwrap(),
}

// ===== 工具宏：逗号分隔 =====
Comma<T>: Vec<T> = {
  <mut v: (<T> ",")*> <e: T?> => match e {
    None => v,
    Some(e) => { v.push(e); v }
  }
};