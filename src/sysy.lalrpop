// lalrpop 里的约定
grammar;

use crate::frontend::ast::*;

// 约束 lexer 的行为
match {
  // 跳过空白符和注释
  r"\s*" => {},
  r"//[^\n\r]*[\n\r]*" => {},
  // 添加多行注释的处理
  r"/\*([^*]|\*[^/])*\*/" => {},
  // 剩下的情况采用默认方式处理
  _
}

pub CompUnit: CompUnit = <func_def: FuncDef> => CompUnit { func_def };

FuncDef: FuncDef = {
  <func_type: FuncType> <ident: Ident> "(" ")" <block: Block> => {
    FuncDef { func_type, ident, block }
  }
}

FuncType: FuncType = "int" => FuncType::Int;

Block: Block = "{" <items: BlockItem*> "}" => Block { items };

BlockItem: BlockItem = {
  <decl: Decl> => BlockItem::Decl(decl),
  <stmt: Stmt> => BlockItem::Stmt(stmt),
};

Decl: Decl = {
  <const_decl: ConstDecl> => Decl::Const(const_decl),
  <var_decl: VarDecl> => Decl::Var(var_decl),
};

ConstDecl: ConstDecl = {
  "const" <btype: BType> <defs: Comma<ConstDef>> ";" => {
    ConstDecl { btype, defs }
  }
};

VarDecl: VarDecl = {
  <btype: BType> <defs: Comma<VarDef>> ";" => {
    VarDecl { btype, defs }
  }
};

BType: BType = "int" => BType::Int;

ConstDef: ConstDef = {
  <ident: Ident> "=" <init: ConstInitVal> => ConstDef { ident, init },
};

VarDef: VarDef = {
  <ident: Ident> => VarDef { ident, init: None },
  <ident: Ident> "=" <init: InitVal> => VarDef { ident, init: Some(init) },
};

ConstInitVal: ConstInitVal = {
  <exp: ConstExp> => ConstInitVal::Exp(exp),
};

InitVal: InitVal = {
  <exp: Exp> => InitVal::Exp(exp),
};

ConstExp: Exp = Exp;

Stmt: Stmt = {
  "return" <exp: Exp> ";" => Stmt::Return(exp),
  <lval: LVal> "=" <exp: Exp> ";" => Stmt::Assign { lval, exp },
};


// ------------ Expression Parsing ------------

Exp: Exp = LOrExp;

// LOrExp := LAndExp | LOrExp "||" LAndExp
LOrExp: Exp = {
  <lhs: LOrExp> "||" <rhs: LAndExp> => Exp::Binary { op: BinaryOp::Or, lhs: Box::new(lhs), rhs: Box::new(rhs) },
  <a: LAndExp> => a,
};

// LAndExp := EqExp | LAndExp "&&" EqExp
LAndExp: Exp = {
  <lhs: LAndExp> "&&" <rhs: EqExp> => Exp::Binary { op: BinaryOp::And, lhs: Box::new(lhs), rhs: Box::new(rhs) },
  <e: EqExp> => e,
};

// EqExp := RelExp | EqExp ("==" | "!=") RelExp
EqExp: Exp = {
  <lhs: EqExp> "==" <rhs: RelExp> => Exp::Binary { op: BinaryOp::Eq, lhs: Box::new(lhs), rhs: Box::new(rhs) },
  <lhs: EqExp> "!=" <rhs: RelExp> => Exp::Binary { op: BinaryOp::Ne, lhs: Box::new(lhs), rhs: Box::new(rhs) },
  <r: RelExp> => r,
};

// RelExp := AddExp | RelExp ("<" | ">" | "<=" | ">=") AddExp
RelExp: Exp = {
  <lhs: RelExp> "<"  <rhs: AddExp> => Exp::Binary { op: BinaryOp::Lt, lhs: Box::new(lhs), rhs: Box::new(rhs) },
  <lhs: RelExp> ">"  <rhs: AddExp> => Exp::Binary { op: BinaryOp::Gt, lhs: Box::new(lhs), rhs: Box::new(rhs) },
  <lhs: RelExp> "<=" <rhs: AddExp> => Exp::Binary { op: BinaryOp::Le, lhs: Box::new(lhs), rhs: Box::new(rhs) },
  <lhs: RelExp> ">=" <rhs: AddExp> => Exp::Binary { op: BinaryOp::Ge, lhs: Box::new(lhs), rhs: Box::new(rhs) },
  <rhs: AddExp> => rhs,
};

AddExp: Exp = {
  <lhs: MulExp> "+" <rhs: AddExp> => Exp::Binary { op: BinaryOp::Add, lhs: Box::new(lhs), rhs: Box::new(rhs) },
  <lhs: MulExp> "-" <rhs: AddExp> => Exp::Binary { op: BinaryOp::Sub, lhs: Box::new(lhs), rhs: Box::new(rhs) },
  <m: MulExp> => m,
}

MulExp: Exp = {
  <lhs: MulExp> "*" <rhs: UnaryExp> => Exp::Binary { op: BinaryOp::Mul, lhs: Box::new(lhs), rhs: Box::new(rhs) },
  <lhs: MulExp> "/" <rhs: UnaryExp> => Exp::Binary { op: BinaryOp::Div, lhs: Box::new(lhs), rhs: Box::new(rhs) },
  <lhs: MulExp> "%" <rhs: UnaryExp> => Exp::Binary { op: BinaryOp::Mod, lhs: Box::new(lhs), rhs: Box::new(rhs) },
  <u: UnaryExp> => u,
}

UnaryExp: Exp = {
  <p: PrimaryExp> => p,
  <op: UnaryOp> <rhs: UnaryExp> => Exp::Unary { op, exp: Box::new(rhs) },
}

PrimaryExp: Exp = {
  "(" <exp: Exp> ")" => exp,
  <num: Number> => Exp::Number(num),
  <lval: LVal> => Exp::LVal(lval),
};

LVal: LVal = {
  <ident: Ident> => LVal::Var(ident),
};

UnaryOp: UnaryOp = { 
  "+" => UnaryOp::Pos,
  "-" => UnaryOp::Neg,
  "!" => UnaryOp::Not,
}

Number: i32 = <num: IntConst> => num;

// 如果匹配到标识符, 就返回这个字符串
// 一对尖括号在此处指代的是正则表达式匹配到的字符串 (&str)
// 关于尖括号到底代表什么, 请 RTFM
Ident: String = r"[_a-zA-Z][_a-zA-Z0-9]*" => <>.to_string();

// 对整数字面量的处理方式: 把匹配到的字符串按对应进制转换成数字
IntConst: i32 = {
  r"[1-9][0-9]*" => i32::from_str_radix(<>, 10).unwrap(),
  r"0[0-7]*" => i32::from_str_radix(<>, 8).unwrap(),
  r"0[xX][0-9a-fA-F]+" => i32::from_str_radix(&<>[2..], 16).unwrap(),
}

// ===== 工具宏：逗号分隔 =====
Comma<T>: Vec<T> = {
  <mut v: (<T> ",")*> <e: T?> => match e {
    None => v,
    Some(e) => { v.push(e); v }
  }
};